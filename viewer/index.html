<!DOCTYPE html>

<style>

#container {
display: flex;
flex-flow: column nowrap;
justify-content: flex-start;
align-items: center;
}

body {
	margin: 0;
	background: #222;
}

#botonera {
	background: #666;
	padding: 1em;
	margin: 0.3em;
	border-radius: 3px;
	border: 1px solid #888;
	box-shadow: 0 1em 1em black;
}

</style>

<div id=container>
	<canvas id=cnv width=400 height=400></canvas>
	<div id=botonera>
		<textarea id=text-shader cols=68 rows=8>
vec3 v0 = pos;
vec3 v1 = (v0 - clamp(v0, vec3(-5.0e-2, -0.25, -9.999998e-3), vec3(5.0e-2, 0.25, 9.999998e-3)));
vec3 v2 = (abs(v0) - vec3(5.0e-2, 0.25, 9.999998e-3));
float v3 = ((length(v1) + min(0.0, max(max(v2.x, v2.y), v2.z))) - 5.0e-2);
vec3 v4 = (v0 - vec3(-7.0e-2, 0.26, -1.5e-2));
vec3 v5 = (v0 - vec3(-2.0e-2, 0.29, -1.0e-2));
vec3 v6 = (v0 - vec3(4.5e-2, 0.28, -1.0e-2));
vec3 v7 = (v0 - vec3(0.1, 0.24, -2.0e-2));
float v8 = min((length(v4) - 6.0e-2), min((length(v5) - 7.0e-2), min((length(v6) - 7.0e-2), (length(v7) - 5.0e-2))));
float v9 = clamp((0.5 + ((0.5 * (v8 - v3)) / 3.0e-2)), 0.0, 1.0);
vec3 v10 = (v0 - vec3(0.0, 0.0, -5.0e-2));
vec3 v11 = (v10 - vec3(-7.0e-2, 0.26, -1.0e-2));
vec3 v12 = (v10 - vec3(-2.0e-2, 0.29, -1.0e-2));
vec3 v13 = (v10 - vec3(4.5e-2, 0.28, -1.0e-2));
vec3 v14 = (v10 - vec3(0.1, 0.24, -6.0e-3));
vec3 v15 = (v0 - vec3(0.0, 0.2, -1.0e-2));
float v16 = (length(v15) - 7.0e-2);
vec3 v17 = (v15 - vec3(-4.0e-2, -4.0e-2, -2.5e-2));
vec3 v18 = (v15 - vec3(4.0e-2, -4.0e-2, -2.5e-2));
float v19 = min((length(v17) - 4.0e-2), (length(v18) - 4.0e-2));
float v20 = clamp((0.5 + ((0.5 * (v19 - v16)) / 2.0e-2)), 0.0, 1.0);
return min((mix(v8, v3, v9) - ((3.0e-2 * v9) * (1.0 - v9))), min(min((length(v11) - 3.0e-2), min((length(v12) - 3.0e-2), min((length(v13) - 3.0e-2), (length(v14) - 3.0e-2)))), (mix(v19, v16, v20) - ((2.0e-2 * v20) * (1.0 - v20)))));
		</textarea>
		<button id=btn-dibujar>DIBUJAR</button>
	</div>
</div>

<script id="vshader" type="x-shader/x-vertex">

    uniform mat4 u_ProjectionMatrix;

    attribute vec3 vNormal;
    attribute vec4 vTexCoord;
    attribute vec4 vPosition;

    varying vec2 v_texCoord;

    void main()
    {
        gl_Position = u_ProjectionMatrix * vPosition + vec4(vNormal * 0.00000001, 0.0);
        v_texCoord = vTexCoord.st;
    }

</script>

<script id="fshader" type="x-shader/x-fragment">

    precision mediump float;


	uniform float angle;

    varying vec2 v_texCoord;

	const vec3 sky_color = vec3(0.7, 0.8, 1.0) * 0.2;
	const vec3 sky_direction = vec3(0.0, 1.0, 0.0);

	float sdf1(vec3 pos) {
		vec3 v0 = pos;
		vec3 v1 = (v0 - clamp(v0, vec3(-5.0e-2, -0.25, -9.999998e-3), vec3(5.0e-2, 0.25, 9.999998e-3)));
		vec3 v2 = (abs(v0) - vec3(5.0e-2, 0.25, 9.999998e-3));
		float v3 = ((length(v1) + min(0.0, max(max(v2.x, v2.y), v2.z))) - 5.0e-2);
		vec3 v4 = (v0 - vec3(-7.0e-2, 0.26, -1.5e-2));
		vec3 v5 = (v0 - vec3(-2.0e-2, 0.29, -1.0e-2));
		vec3 v6 = (v0 - vec3(4.5e-2, 0.28, -1.0e-2));
		vec3 v7 = (v0 - vec3(0.1, 0.24, -2.0e-2));
		float v8 = min((length(v4) - 6.0e-2), min((length(v5) - 7.0e-2), min((length(v6) - 7.0e-2), (length(v7) - 5.0e-2))));
		float v9 = clamp((0.5 + ((0.5 * (v8 - v3)) / 3.0e-2)), 0.0, 1.0);
		vec3 v10 = (v0 - vec3(0.0, 0.0, -5.0e-2));
		vec3 v11 = (v10 - vec3(-7.0e-2, 0.26, -1.0e-2));
		vec3 v12 = (v10 - vec3(-2.0e-2, 0.29, -1.0e-2));
		vec3 v13 = (v10 - vec3(4.5e-2, 0.28, -1.0e-2));
		vec3 v14 = (v10 - vec3(0.1, 0.24, -6.0e-3));
		vec3 v15 = (v0 - vec3(0.0, 0.2, -1.0e-2));
		float v16 = (length(v15) - 7.0e-2);
		vec3 v17 = (v15 - vec3(-4.0e-2, -4.0e-2, -2.5e-2));
		vec3 v18 = (v15 - vec3(4.0e-2, -4.0e-2, -2.5e-2));
		float v19 = min((length(v17) - 4.0e-2), (length(v18) - 4.0e-2));
		float v20 = clamp((0.5 + ((0.5 * (v19 - v16)) / 2.0e-2)), 0.0, 1.0);
		return min((mix(v8, v3, v9) - ((3.0e-2 * v9) * (1.0 - v9))), min(min((length(v11) - 3.0e-2), min((length(v12) - 3.0e-2), min((length(v13) - 3.0e-2), (length(v14) - 3.0e-2)))), (mix(v19, v16, v20) - ((2.0e-2 * v20) * (1.0 - v20)))));
	}

	float sdf(vec3 pos, mat3 transform) {
		pos = transform * pos;
		float scale = 10.0;
		return sdf1((pos + vec3(0.0, 1.5, 0.0)) / scale) * scale;
	}

	vec4 raymarch(vec3 pos, vec3 dir, mat3 transform) {

		float t = 1.0;
		for (int i = 0; i < 70; ++i) {
			float dist = sdf(pos + dir * t, transform);
			if (dist < 0.0001 * t) {
				return vec4(pos + dir * t, 1.0);
			}
			t += dist;
		}

		return vec4(pos, 0.0);
	}

	vec4 view_raymarch(vec3 dir, mat3 transform) {
		return raymarch(vec3(0.0, 0.0, -5.0), dir, transform);
	}

	vec3 compute_normal(vec3 p, mat3 A) {
		const float h = 0.0001;
		const vec3 k = vec3(1.0, -1.0, 0.0);
		// return normalize(
		// k.xzz * (sdf(p+k.xzz*h, A) - sdf(p-k.xzz*h, A)) +
		// k.zxz * (sdf(p+k.zxz*h, A) - sdf(p-k.zxz*h, A)) +
		// k.zzx * (sdf(p+k.zzx*h, A) - sdf(p-k.zzx*h, A)) );
		return normalize(
			k.xyy * sdf(p+k.xyy*h, A) +
			k.yyx * sdf(p+k.yyx*h, A) +
			k.yxy * sdf(p+k.yxy*h, A) +
			k.xxx * sdf(p+k.xxx*h, A) );
	}

	vec3 linear_to_gamma(vec3 color) {
		float level = 1.0 / 2.2;
		return vec3(
			pow(color.x, level),
			pow(color.y, level),
			pow(color.z, level));
	}

	vec3 gamma_to_linear(vec3 color) {
		float level = 2.2;
		return vec3(
			pow(color.x, level),
			pow(color.y, level),
			pow(color.z, level));
	}

	float blinn_phong(vec3 n, vec3 l, vec3 v, float exponent) {
		vec3 h = normalize(l + v);
		float normalization = (exponent + 1.0) * 0.15915494309;
		float intensity = pow(max(dot(n, h), 0.0), exponent);
		return intensity * normalization;
	}

	float lambert(vec3 n, vec3 l) {
		return max(dot(n, l), 0.0) * 0.31830988618;
	}

	float wide_lambert(vec3 n, vec3 l) {
		return dot(n, l) * 0.5 + 0.5;
	}

	float schlick(vec3 n, vec3 v) {
		float curve = pow(1.0 - dot(n, v), 5.0);
		float base = 0.04;
		return mix(base, 1.0, curve);
	}

	float soft_visiblity(vec3 pos, vec3 dir, mat3 transform, float sharpness) {

		float res = 1.0;
		float t = 0.02;

		for (int i = 0; i < 128; ++i) {
			float dist = sdf(pos + dir * t, transform );
			if (dist < 0.0001) return 0.0;
			res = min(res, dist / (t * sharpness));
			t += dist;
		}

		return res;
	}

	vec3 shade(vec3 pos, vec3 n, vec3 v, mat3 transform) {

		vec3 sky_power = 1.0 * gamma_to_linear(sky_color);
		vec3 sky_diffuse = sky_power * wide_lambert(n, sky_direction);
		vec3 sky_specular = sky_power * 1.0;

		vec3 lamp1_dir = normalize(vec3(1.0, 0.5, -0.3));
		vec3 lamp1_color = vec3(0.96, 0.83, 0.51) * 0.0 + 1.0;
		vec3 lamp1_power = 10.0 * gamma_to_linear(lamp1_color) * soft_visiblity(pos, lamp1_dir, transform, 0.1);
		vec3 lamp1_diffuse = lamp1_power * vec3(lambert(n, lamp1_dir));
		vec3 lamp1_specular = lamp1_power * vec3(blinn_phong(n, lamp1_dir, v, 34.0));

		vec3 lamp2_dir = normalize(vec3(-1.0, 0.1, -0.2));
		vec3 lamp2_color = vec3(0.96, 0.83, 0.51);
		vec3 lamp2_power = 0.2 * gamma_to_linear(lamp2_color);
		vec3 lamp2_diffuse = lamp2_power * vec3(wide_lambert(n, lamp2_dir));
		vec3 lamp2_specular = lamp2_power * vec3(blinn_phong(n, lamp2_dir, v, 8.0));

		vec3 lamp3_dir = normalize(vec3(-0.4, -0.1, 0.3));
		vec3 lamp3_color = vec3(0.2, 0.7, 0.5);
		vec3 lamp3_power = 0.5 * gamma_to_linear(lamp3_color);
		vec3 lamp3_diffuse = lamp3_power * vec3(lambert(n, lamp3_dir));
		vec3 lamp3_specular = lamp3_power * vec3(blinn_phong(n, lamp3_dir, v, 15.0));

		float fresnel = schlick(n, v);

		vec3 color = vec3(1.0, 0.8, 0.3);
		vec3 albedo = gamma_to_linear(color);
		vec3 diffuse = albedo * (lamp1_diffuse + lamp2_diffuse + lamp3_diffuse + sky_diffuse);

		vec3 specular = lamp1_specular + lamp2_specular + lamp3_specular + 0.6 * sky_specular;

		return mix(diffuse, specular, fresnel);
	}

	void main() {
		float ca = cos(angle);
		float sa = sin(angle);
		mat3 transform = mat3(
			ca, 0, -sa,
			 0, 1,   0,
			sa, 0,  ca
		);

		vec2 rectCoord = (v_texCoord.st * 2.0 - 1.0) * 0.5;

		vec3 v = -normalize(vec3(rectCoord, 1.0));

		vec4 res = view_raymarch(-v, transform);
		if (res.a < 0.5) {
			gl_FragColor = vec4(sky_color, 1.0);
		} else {
			vec3 n = compute_normal(res.xyz, transform);
			vec3 light = shade(res.xyz, n, v, transform);
			vec3 tonemapped = light / (light + 1.0);
			gl_FragColor = vec4(linear_to_gamma(tonemapped), 1.0);
			// gl_FragColor = vec4(linear_to_gamma(n*0.5+0.5), 1.0);
		}
	}

</script>


<script src=J3DI.js></script>
<script src=J3DIMath.js></script>
<script src=webgl-utils.js></script>
<script src=main.js></script>
